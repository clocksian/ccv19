buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.2"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.8"
    }
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

description = 'Kotlin extensions for DbSetup'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.0.2"
    compile project(':DbSetup')
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.9.0'
}

dokka {
    inputs.dir 'src/main/kotlin'
    jdkVersion = 8
    inputs.dir 'config/dokka'
    doLast {
        copy {
            from 'config/dokka'
            include 'style.css'
            into outputDirectory
        }
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    inputs.dir 'src/main/kotlin'
    jdkVersion = 8
}

task dokkaJar(type: Jar, dependsOn: dokka) {
    classifier = 'dokka'
    from dokka.outputDirectory
    manifest {
        from project.ext.sharedManifest
    }
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    manifest {
        from project.ext.sharedManifest
    }
}

artifacts {
    archives javadocJar
    archives dokkaJar
}

checkstyleMain {
    source sourceSets.main.kotlin
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact dokkaJar
        }
    }
}
